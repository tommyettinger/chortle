buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.36.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

def projectName = 'chortle'

group = 'com.github.tommyettinger'

version = "$VERSION_NAME" // You can set the version in gradle.properties .

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

// This just makes sure that a Javadoc JAR and a sources JAR are produced.
mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

// This sets the Javadoc JAR to have hopefully the correct name. This may need adjustment.
plainJavadocJar.archiveBaseName.set(projectName)

// There's pretty much no reason to use any encoding other than UTF-8 .
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:none,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

javadoc {
    destinationDir = file('docs/apidocs')
    options {
        links('https://javadoc.io/doc/com.badlogicgames.gdx/gdx/latest/')
    }
}

apply plugin: 'idea'
// This makes IDEA avoid including generated JavaDocs (which are HTML files) in any search results.
idea.module.excludeDirs += [file("docs/")]

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation ("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"){
        exclude group: "org.jcraft", module: "jorbis"
        exclude group: "org.lwjgl", module: "lwjgl-openal"
    }
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    testImplementation "junit:junit:4.13.2"
}
